/*******************************************************************************
 * Copyright (c) 2022 Martin Erich Jobst
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Martin Jobst - initial API and implementation and/or initial documentation
 *******************************************************************************/
module org.eclipse.fordiac.ide.structuredtext.mwe.GenerateSTAlgorithm

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.fordiac.ide.structuredtextalgorithm"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*******************************************************************************
 * Copyright (c) 2022, 2024 Martin Erich Jobst
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *   Martin Jobst - initial API and implementation and/or initial documentation
 *******************************************************************************/"
				preferXtendStubs = false
			}
		}
		language = XtextGeneratorLanguage {
			name = "org.eclipse.fordiac.ide.structuredtextalgorithm.STAlgorithm"
			fileExtensions = "stalg"
			referencedResource = "platform:/resource/org.eclipse.fordiac.ide.model/model/fordiac.genmodel"
			referencedResource = "platform:/resource/org.eclipse.fordiac.ide.structuredtextcore.model/model/STCore.genmodel"
			referencedResource =
			"platform:/resource/org.eclipse.fordiac.ide.structuredtextalgorithm.model/model/STAlgorithm.genmodel"
			fragment = grammarAccess.GrammarAccessFragment2 {}
			fragment = ecore.EMFGeneratorFragment2 {}
			fragment = serializer.SerializerFragment2 {}
			fragment = resourceFactory.ResourceFactoryFragment2 {}
			fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
				options = {
					ignoreCase = true
				}
			}
			fragment = validation.ValidatorFragment2 {}
			fragment = scoping.ImportNamespacesScopingFragment2 {
				ignoreCase = true
			}
			fragment = exporting.QualifiedNamesFragment2 {}
			fragment = builder.BuilderIntegrationFragment2 {}
			fragment = index.ResourceDescriptionStrategyFragment {}
			fragment = ui.labeling.LabelProviderFragment2 {}
			fragment = ui.outline.QuickOutlineFragment2 {}
			fragment = ui.outline.OutlineTreeProviderFragment2 {
				generateStub = true
			}
			fragment = ui.quickfix.QuickfixProviderFragment2 {
				generateStub = true
			}
			fragment = ui.contentAssist.ContentAssistFragment2 {
				generateStub = false
			}
			fragment = ui.refactoring.RefactorElementNameFragment2 {
				useChangeSerializer = true
			}
			fragment = ui.templates.CodetemplatesGeneratorFragment2 {}
			fragment = formatting.Formatter2Fragment2 {
				generateStub = true
				generateXtendStub = true
			}
			fragment = ui.compare.CompareFragment2 {}
			fragment = junit.JUnitFragment {
				junitVersion = "5"
			}
			fragment = ui.codemining.CodeMiningFragment {
				generateStub = true
				generateXtendStub = true
			}
		}
	}
}
